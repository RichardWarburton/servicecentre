apply(plugin: 'java')
apply(plugin: 'maven')
apply(plugin: 'signing')

group = 'com.toao'
version = '0.1.0'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}
 
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

signing {
    sign(configurations.archives);
}

artifacts {
    archives(jar);
    archives(javadocJar);
    archives(sourcesJar);
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'com.google.inject:guice:3.0',
            'org.slf4j:slf4j-api:1.7.10',
            'ch.qos.logback:logback-classic:1.1.2',
            'com.google.guava:guava:18.0',
            'junit:junit:4.12',
            'org.mockito:mockito-all:1.10.19',
            'com.google.inject.extensions:guice-multibindings:3.0')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

if( hasProperty('uploadArchives') )
{
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
     
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                  authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
     
                pom.project {
                   name 'ServiceCentre'
                   packaging 'jar'
                   description 'Basic lifecycle management for Guava Services using Guice'
                   url 'https://github.com/sadiqj/servicecentre'
     
                   scm {
                       url 'scm:git@github.com:sadiqj/servicecentre.git'
                       connection 'scm:git@github.com:sadiqj/servicecentre.git'
                       developerConnection 'scm:git@github.com:sadiqj/servicecentre.git'
                   }
     
                   licenses {
                       license {
                           name 'The Apache Software License, Version 2.0'
                           url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                           distribution 'repo'
                       }
                   }
     
                   developers {
                       developer {
                           id 'sadiqj'
                           name 'Sadiq Jaffer'
                       }
                   }
               }
            }
        }
    }
}
